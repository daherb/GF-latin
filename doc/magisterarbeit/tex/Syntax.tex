Den letzten Teil dieser Arbeit bildet der Bereich der Syntax, um aus den einfachen Einheiten aus dem Lexikon komplexe Einheiten von einfacheren Phrasen bis hin zu kompletten Sätzen zu bilden. Zunächst wird erläutert, wie einfache Phrasen konstruiert werden. Und diese Phrasen wiederum werden weiter kombiniert, um größere Ausdrücke zu erzeugen, bis hin zur Satzebene, bzw. zu den Äußerungen (Utterances, \texttt{Utt}) in der Nomenklatur des Grammatical Frameworks.
\subsection{Nominalphrasen}
\label{subsec:nominalphrasen}
Als erstes sollen nun die verschiedenen Möglichkeiten beschrieben werden, eine Nominalphrase zu konstruieren. Dies geschieht mit Hilfe von Regeln die in der Datei \textbf{NounLat.gf} zu finden sind. \par
\begin{lstlisting}[float=h!tp,caption={Die Syntaxregel \texttt{DetCN}, um ein Determinans und ein Common Nouns zu einer \texttt{NP} zu verbinden (vgl. \textbf{NounLat.gf}},label={GF-Noun-DetCN}]
lin
  DetCN det cn = -- Det -> CN -> NP
    {
      s = \\c => 
        det.s ! cn.g ! c ++ 
        cn.preap.s ! (Ag cn.g det.n c) ++ 
        cn.s ! det.n ! c ++ 
        cn.postap.s ! (Ag cn.g det.n c) ; 
      n = det.n ;  
      g = cn.g ; 
      p = P3 ;
    } ;
\end{lstlisting}
Die erste Regel namens \texttt{DetCN} gibt an, dass ein Determinans zusammen mit einer Phrase von Typ \texttt{CN} (Common Noun) zu einer Nominalphrase verbunden werden kann. Allerdings haben wir bisher noch nicht besprochen, wie Common Nouns aufgebaut sind und wie man sie erzeugt. Common Nouns sind, möglicherweise durch Adjektive attribuierte, Nomen. Deshalb sind sie im Grunde aufgebaut wie Nomen, haben also ein Feld für das inhärente Genus und ein \texttt{s}-Feld für die Wortformen. Zusätzlich hat ein Common Noun aber auch zwei Felder, in denen Adjektive bzw. genauer Adjektivphrasen, die das Nomen näher beschreiben, abgelegt werden können. Da für Adjektive in der lateinischen Sprache keine klaren Regeln herrschen, ob sie vor oder nach dem Nomen stehen, auf das sie Bezug nehmen, gibt es für jede der beiden Positionen je ein Feld, in das Adjektive flexibel angefügt werden können.\footnote{vgl. \cite{BAYER-LINDAUER1994} S. 118} \par
Adjektivphrasen und Adjektiv können in diesem Falle synonym verwendet werden, denn Adjektivphrasen unterscheiden sich aktuell nur soweit von Adjektiven, dass sie statt verschiedener Steigerungsformen nur in der Positiv-Form vorkommen. Das heißt, um aus einem Adjektiv eine Adjektivphrase zu erzeugen werden lediglich die Positiv-Formen des Adjektivs ausgewählt.\footnote{vgl. Regel \texttt{PositA} in \textbf{AdjectiveLat.gf}} Adjektivphrasen können allerdings auch per Konjunktion verbunden werden um daraus wieder eine neue Adjektivphrase zu bilden. Dies geschieht mit Hilfe der Regel \texttt{ConjAP} in der Datei \textbf{ConjunctionLat.gf}. Sie erzeugt aus einer Konjunktion wie \texttt{and\_Conj} oder \texttt{or\_Cat} und einer Liste von Adjektivphrasen eine Adjektivphrase. \par
\begin{lstlisting}[float=h!tp,caption={Der Listentyp Für Adjektivphrasen und die Funktionen zum Erstellen und Verwenden (vgl. \textbf{ConjunctionLat.gf}},label={GF-Conjunction-AP}]
lincat
  [AP] = {s1,s2 : Agr => Str } ;
lin
  ConjAP conj ss = 
    conjunctDistrTable Agr conj ss ;
  BaseAP x y = 
    lin A ( twoTable Agr x y ) ;
  ConsAP xs x = 
    lin A ( consrTable Agr and_Conj.s2 xs x );
\end{lstlisting}
Dazu muss kurz erklärt werden, wie Listen im Grammatical Framework erzeugt werden könne. Im Grammatical Framework werden Listentypen als Typen in eckigen Klammern geschrieben. So ist \texttt{[AP]} eine Liste von Objekten des Typs \texttt{AP}. Die Liste der Adjektivphrasen ist als ein Verbund von zwei Feldern definiert. Das zweite davon enthält das letzte Element der Liste und das erste alle davor befindlichen Elemente, die schon durch Konjunktionen verbunden sind. Diese Listen werden mit folgenden zwei Regeln aufgebaut. Die Regel \texttt{BaseAP} erzeugt eine zweielementige Liste aus zwei Adjektivphrasen. Dazu wird eine interne Funktion namens \texttt{twoTables}\footnote{vgl. \textbf{Coordination.gf} in \textbf{lib/src/prelude}} verwendet, die den Verbund erstellt und den richtigen Typ festlegt. Die zweite Listenoperation ist die Funktion \texttt{ConsAP}, die an eine bestehende Liste ein Element anfügt. Dazu wird das ehemals letzte Element zunächst an den Rest angehängt. Zur Verbindung wird eine fixe Zeichenkette verwendet, in diesem Falle pauschal die Konjunktion \texttt{and\_Conj}. Mit diesen Regeln können Adjektivphrasen erstellt werden, die aus mehreren einzelnen \texttt{AP}s bestehen, die mit Konjunktionen verbunden sind. Die beiden Funktionen \texttt{consrTable} und \texttt{conjunctDistrTable} sind wie \texttt{twoTable} Hilfsfunktionen, die direkt vom Grammatical Framework bereitgestellt werden um die Listenhandhabung zu ermöglichen. \par
\begin{lstlisting}[float=h!tp,caption={Die Syntaxregeln \texttt{UseN} und \texttt{UseN2} um ein Nomen als \texttt{CN} zu verwenden (vgl. \textbf{NounLat.gf}},label={GF-Noun-UseNs}]
lin
  UseN n = -- N -> CN
    lin CN ( n ** {preap, postap = {s = \\_ => "" } } ) ;
  UseN2 n2 = -- N2 -> CN
    lin CN ( n2 ** {preap, postap = {s = \\_ => "" } } ) ;
\end{lstlisting}
\begin{lstlisting}[float=h!tp,caption={Die Syntaxregeln \texttt{AdjCN} um ein Common Noun mit einer Adjektivphrase zu erweitern (vgl. \textbf{NounLat.gf}},label={GF-Noun-AdjCN}]
param
  AdjPos = Pre | Post ;
lin
  AdjCN ap cn =  -- AP -> CN -> CN
    let pos = variants { Post ; Pre }
    in
    {
      s = cn.s ; g = cn.g
      postap = case pos of { 
        Pre => cn.postap ; 
        Post => 
          { s = \\a => ap.s ! a ++ cn.postap.s ! a } } ;
      preap = case pos of { 
        Pre => 
          { s = \\a => ap.s ! a ++ cn.preap.s ! a } ; 
        Post => cn.preap } ;
    } ;
\end{lstlisting}
Die einfachste Möglichkeit Common Nouns zu bilden ist es, einfach Nomen als \texttt{CN}s zu verwenden. Dazu gibt es die beiden Regeln \texttt{UseN} und \texttt{UseN2}. Sie erweitern die beiden Nomentypen \texttt{N} und \texttt{N2} lediglich um die beiden leeren \texttt{AP}-Felder um sie in Common Nouns zu verwandeln. Diese Common Nouns können dann durch Adjektive genauer bestimmt werden. Dazu wird in der Regel \texttt{AdjCN} über freie Variation ausgewählt, ob die modifizierende Adjektivphrase vor oder hinter das Common Noun eingefügt wird. Entsprechend der so gewählten Position wird die \texttt{AP} in eines der beiden vorgesehenen Felder eingefügt. Das \texttt{s}-Feld für die Nomen-Formen so wie das Nomen-Genus werden dabei einfach übernommen. \par
\begin{lstlisting}[float=h!tp,caption={Die Syntaxregeln \texttt{DetQuant} um aus einem \texttt{Num}-Element und einem Quantifikator ein \texttt{Det} zu erzeugen (vgl. \textbf{NounLat.gf}},label={GF-Noun-DetQuant}]
lin
  NumSg = { s = \\_,_ => [] ; n = Sg } ;
  NumPl = { s = \\_,_ => [] ; n = Pl } ;
  DefArt = { s = \\_ => [] ; sp = \\_ => [] } ;
  IndefArt = { s = \\_ => [] ; sp = \\_ => [] } ;
  DetQuant quant num = {
    s  = \\g,c => 
      quant.s  ! Ag g num.n c ++ num.s ! g ! c ;
    sp = \\g,c => 
      quant.sp ! Ag g num.n c ++ num.s ! g ! c ;
    n  = num.n
  };
\end{lstlisting}
Als nächstes fehlen für die \texttt{DetCN}-Regel die Determinantia. Einige Wörter vom Typ \texttt{Det} sind im Lexikon definiert. Die Determinantia, die in den meisten Sprachen am häufigsten vorkommen, sind dort allerdings nicht zu finden, der bestimmte und der unbestimmte Artikel. Stattdessen werden sie direkt bei den Regeln für die Nominalphrasen definiert. Allerdings existieren diese in der lateinischen Sprache im üblichen Sinne überhaupt nicht. Deswegen sind sie Quantifikatoren mit leeren Zeichenketten. Quantifikatoren können mit Hilfe der Regel \texttt{DetQuant} zu einem Determinans umgewandelt werden. Dazu wird zusätzlich ein so genanntes "`number determining element"', kurz \texttt{Num}, als Marker für den Numerus verwendet. Denn in verschiedenen Grammatiktheorien, unter anderem der Theorie der Transformationsgrammatik, gilt der Numerus als festes Merkmal des Determinans, das das Nomen in diesem Merkmal regiert\footnote{vgl. \cite{METZLER2004} S. 6706}. So erhält man für die bestimmten und unbestimmten Artikel vier verschiedene Objekte der Kategorie \texttt{Det}, die alle die leere Zeichenkette erzeugen, und von denen zwei das Merkmal des Singular und zwei den Plural tragen. Bestimmte und unbestimmte Artikel sind dabei nicht wirklich zu unterscheiden. \par
Damit haben wir alle nötigen Bestandteile für die \texttt{DetCN}-Regel (Listing \ref{GF-Noun-DetCN}). Diese Regel funktioniert nun folgendermaßen. Im \texttt{s}-Feld ist die Zeichenkette enthalten, die durch die Phrase erzeugt werden kann. Da diese weiterhin vom Kasus abhängig ist, wird eine Tabelle erzeugt, wobei der später zur Auswahl genutzte Wert in der Variable \texttt{c} zur Verfügung steht. Diese Zeichenkette besteht aus der Determinans-Form, die von Genus und Kasus abhängt. Das Genus wird vom \texttt{g}-Feld des Common Noun bestimmt, der Kasus stammt aus der Variable \texttt{c}. Auf die so entstandene Form folgen die Adjektivphrasen, die vor dem Nomen platziert sind. Diese stammen aus dem Feld \texttt{preap} des Common Nouns und müssen mit dem Nomen in Genus, Numerus und Kasus übereinstimmen. Dazu wird, unter anderem um keine dreifach geschachtelte Tabelle zu benötigen, ein so genannter abhängiger Typ für die Kongruenz zwischen Nomen und Adjektiven verwendet. Der Typ heißt \texttt{Agr} für Agreement, also Übereinstimmung, und besteht aus einem Konstruktor, also einer Art Schlüsselwort wie hier \texttt{Ag} und einer Liste von Typen. In diesem Falle sind das Genus, Numerus und Kasus.
Als nächstes folgt die Nomenform, die den Numerus des Determinans übernimmt, so wie den Kasus aus \texttt{c}. Als letztes folgen die Adjektivphrasen nach dem Nomen aus dem Feld \texttt{postap} analog zu \texttt{preap}. Durch die Verkettung dieser Bestandteile wird die Zeichenkette gebildet, die der Nominalphrase entspricht. Diese Nominalphrase behält den Numerus des Artikels so wie das Genus des Common Nouns als inhärente Merkmale. Zusätzlich hat sie als Person die 3. Person, die die Verbform beeinflusst. Dies ist die komplexeste der bisher implementierten Regeln um Nominalphrasen zu erzeugen. \par
\begin{lstlisting}[float=h!tp,caption={Die Syntaxregel \texttt{UsePN}, um einen Eigennamen als \texttt{NP} zu verwenden (vgl. \textbf{NounLat.gf}},label={GF-Noun-UsePN}]
lin
  UsePN pn = 
    lin NP { s = pn.s ! Sg ; g = pn.g ; n = Sg ; p = P3 } ;
\end{lstlisting}
Daneben gibt es noch zwei kurze Regeln um Nominalphrasen aus Eigennamen und Personalpronomen zu erzeugen. Die erste namens \texttt{UsePN} hat die selbe Form wie der verwendete Name im Singular. Entsprechend ist der Genus der \texttt{NP} gleich dem Genus des Eigennamens, der Numerus ist Singular und die Person wieder die 3. Person. \par
\begin{lstlisting}[float=h!tp,caption={Die Syntaxregel \texttt{UsePN} um ein Personalpronomen als \texttt{NP} zu verwenden (vgl. \textbf{NounLat.gf})},label={GF-Noun-UsePron}]
lin
  UsePron p = -- Pron -> Np
  { 
    g = p.g ; n = p.n ; p = p.p ;
    s = \\c => case c of { 
      -- Drop pronoun in nominative case
      Nom => p.pers ! PronDrop ! PronRefl ;
      -- but don't drop it otherwise 
      _ => p.pers ! PronNonDrop ! PronRefl 
    } ! c ;
  } ;
\end{lstlisting}
Und die letzte Regel \texttt{UsePron} verwandelt ein Pronomen in eine Nominalphrase. Dazu werden Genus, Numerus und Person aus dem Pronomen übernommen. Bei der Kasus-abhängigen Form kommt dafür das Pro-Drop-Phänomen zu tragen. Denn wenn die aus einem Pronomen gebildete Nominalphrase im Nominativ verwendet wird, entfällt sie, es wird also nur die leere Zeichenkette produziert. In allen anderen Kasus wird einfach die entsprechende Pronomenform gebildet. 
Soll allerdings auch in der Subjektposition die Pronomenform erzwungen werden, existiert im Extra-Teil\footnote{\textbf{ExtraLatAbs.gf} und \textbf{ExtraLat.gf}} der Grammatik eine Funktion namens \texttt{UsePronNonDrop}, die aus der Nicht-Pro-Drop-Form des Pronomens eine Nominalphrase erzeugt. Der Extra-Teil einer Grammatik kann im Grammatical Framework verwendet werden um für eine einzelne Sprache spezielle Konstrukte zu beschreiben. Diese können zwar beim Parsen verwendet werden, nicht jedoch beim Übersetzen, wenn die Zielsprache nicht auch die selbe erweiterte abstrakte Syntax hat. Allerdings können bei nahe verwandten Sprachen möglicherweise auch die Extra-Konstrukte aufeinander abgebildet werden, so dass auch zwischen diesen Sprachen übersetzt werden kann.\par
Diese drei Regeln bieten die grundlegenden Möglichkeiten um in einer Sprache Nominalphrasen zu bilden, wie sie in einfachen Sätzen Verwendung finden. In der abstrakten Syntax existieren noch fünf weitere Regeln, wovon eine einzige wirklich eine neue \texttt{NP} erzeugt. Die restlichen Regeln modifizieren lediglich die Nominalphrasen. Es verbleiben also diese Regeln für mögliche zukünftige Arbeiten.
\subsection{Verbalphrasen}
\label{subsec:verbalphrasen}
Die zweite Gruppe von wichtigen Phrasen in dieser Grammatik sind die Verbalphrasen. Denn die Sorte einfacher Sätze, die den Abschluss dieser Arbeit bilden, werden durch die Kombination von einer Nominalphrase mit einer Verbalphrase gebildet. Nachdem wir schon wissen, wie Nominalphrasen gebildet werden können, soll nun die Konstruktion von Verbalphrasen in dieser Grammatikimplementierung folgen. \par
Die einfachste Form einer Nominalphrase wird aus einem intransitiven Verb gebildet, also einem Verb, dass kein Objekt benötigt. Die entsprechende Regel heißt \texttt{UseV} und benutzt lediglich eine Hilfsfunktion namens \texttt{predV}. Diese Funktion baut die Datenstruktur, die die für eine Verbalphrase nötigen Felder enthält (Listing \ref{GF-Res-VerbPhrase}). 
\begin{lstlisting}[float=h!tp,caption={Datenstruktur für Verbalphrasen (vgl. \textbf{ResLat.gf})},label={GF-Res-VerbPhrase}]
oper
  VerbPhrase : Type = {
    fin : VActForm => Str ;
    inf : VInfForm => Str ;
    obj : Str ;
    adj : Agr => Str
  } ;
\end{lstlisting}
Dies sind finite Verbformen, infinite Verbformen, wenn vorhanden ein Objekt und möglicherweise auch ein modifizierendes Adjektiv. Die Typen \texttt{VActForm} und \texttt{VInfForm} haben dabei die selben möglichen Werte wie bei den Verben. Die Funktion \texttt{predV} füllt das \texttt{fin}-Feld mit den aktiven Verbformen und das \texttt{inf}-Feld mit den Infinitivformen des Verbs. Die Felder für das Objekt und das Adjektiv werden mit leeren Zeichenketten gefüllt. Damit ist die Verbalphrase auch schon erzeugt. \par
Für transitive Verben ist auf dem Weg zur Verbalphrase noch ein Zwischenschritt nötig. Denn aus einem transitiven Verb wird zunächst eine \texttt{VP} erzeugt, der eine Nominalphrase in der Objektposition fehlt. Diese Kategorie heißt analog zur Nomenklatur im GPSG-Grammatikformalismus VPSlash (in der GPSG-Notation VP/NP).\footnote{vgl. \cite{RANTA2011} S. 217} Solch eine \texttt{VPSlash} besteht lediglich aus einer, um ein zusätzliches Feld erweiterte, Verbalphrase. In diesem zusätzlichen Feld kann, wenn nötig eine Präposition gespeichert werden, die unter anderem bestimmt, in welchem Kasus das Objekt des Verbs stehen muss. Diese bei transitiven Verben nötige Präposition ist üblicherweise im Lexikoneintrag des Verbs zu finden oder ein Akkusativobjekt wird angenommen. Die Transformation eines transitiven Verbs in eine \texttt{VPSlash} erfolgt in der Regel \texttt{SlashV2a} analog zu den intransitiven Verben mit einer Funktion namens \texttt{predV2}, wobei der Typ \texttt{V2} der Typ für transitive Verben ist. Diese Funktion macht nichts anderes um mit Hilfe von \texttt{predV} eine \texttt{VP} zu erzeugen, um die Präposition des transitiven Verbs erweitern und den Typ auf \texttt{VPSlash} zu ändern. \par
\begin{lstlisting}[float=h!tp,caption={Die Syntaxregel \texttt{ComplSlash} um eine \texttt{VPSlash} und eine \texttt{NP} zu einer Verbalphrase zu verbinden (vgl. \textbf{NounLat.gf} und \textbf{ResLat.gf})},label={GF-Noun-ComplSlash}]
oper
  appPrep : Preposition -> (Case => Str) -> Str =
    \c,s -> c.s ++ s ! c.c ;
lin
  ComplSlash vp np = -- VPSlash -> NP -> VP
    insertObj (appPrep vp.c2 np.s) vp ;
\end{lstlisting}
Um nun aus solch einer \texttt{VPSlash} eine vollständige Verbalphrase zu erzeugen, kann die Regel \texttt{ComplSlash} verwendet werden. Sie kombiniert eine \texttt{VPSlash} zusammen mit einer Nominalphrase zu einer \texttt{VP}. Dazu werden zwei Hilfsfunktionen verwendet. Die erste, \texttt{appPrep} verbindet die in der \texttt{VPSlash} hinterlegte Präposition mit der Objekt-\texttt{NP} indem sie die Präposition vor die Form der Nominalphrase im richtigen Kasus setzt. Die so entstandene Zeichenkette wird dann in der Funktion \texttt{insertObj} in das Objekt-Feld der Verbalphrase, vor möglichen weiteren Objekten, eingefügt.
\FloatBarrier
\subsection{Einfache Sätze}
\label{subsec:einfachesaetze}
Nachdem wir uns in den vorangegangenen Abschnitten mit dem Aufbau einzelner Phrasen in der lateinischen Syntax beschäftigt haben, können wir uns nun letztendlich dem Aufbau ganzer Sätze widmen. Das die Wortstellung in der lateinischen Sprache nicht unproblematisch ist, wurde nun schon des öfteren angesprochen. Doch wie die Situation wirklich ist, ist z.B. bei Bamman und Crane (\cite{BAMMAN2006}) zu finden. \par 
In diesem Paper, das sich primär mit der Erstellung einer Treebank für lateinische Texte beschäftigt, sind an Hand der Treebank einige empirische Werte für die Wortstellung in lateinischen Sätzen aufgeführt. Das dazu untersuchte Korpus enthält Textauszüge aus dem Werk vier verschiedener Autoren, nämlich Cicero (ca. 63 v. Chr.), Caesar (ca. 51 v. Chr.), Vergil (ca. 19 v. Chr.) und Hieronymus (ca. 405 n. Chr.). Damit werden sowohl verschiedenste Epochen und verschiedene Stilrichtigungen abgedeckt. So sind Caesar und Hieronymus Autoren relativ einfacher Prosa, Cicero ein bekannter Redner und Rhetoriker und Vergil ein kunstvoller Dichter. Insgesamt umfasst das Korpus 12098 Wörter wovon auf Cicero 1189, auf Caesar 1486, auf Vergil 2647 und auf Hieronymus 6776 Wörter entfallen.\footnote{\cite{BAMMAN2006} S. 71f.} 
Bei der lateinischen Wortstellung geht man davon aus, dass allgemein die Reihenfolge Subjekt-Objekt-Verb bevorzugt wird. Dies verändert sich jedoch hin zur Folge Subjekt-Verb-Objekt, wie sie häufig bei modernen romanischen Sprachen zu finden ist. Caesar verwendet vorwiegend die für das klassische Latein zu erwartende Wortstellung Subjekt-Objekt-Verb (64.7\%), Cicero dagegen benutzt am häufigsten die Wortstellung Objekt-Subjekt-Verb (52.6\%), die allgemein dazu dient, das Objekt zu betonen. Vergil als Lyriker verwendet alle möglichen Wortstellungen relativ häufig um unter anderem der gewünschten lyischen Form Rechnung zu tragen, am häufigsten jedoch wie bei Cicero die Wortstellung Objekt-Subjekt-Verb (25.0\%). Und schließlich Hieronymus verwendet bereits vornehmlich die Wortfolge Subjekt-Verb-Objekt (68.5\%). Betrachtet man zusätzlich diejenigen Sätze ohne Subjekt, also die Sätze bei denen ein Pronomen in der Subjektposition entfallen ist, so stellt man fest, dass die klassischeren Autoren Caesar, Cicero und Vergil klar die Wortstellung  Objekt-Verb bevorzugten. Nur Hieronymus verwendet meistens die Wortstellung Verb-Objekt.\footnote{\cite{BAMMAN2006} S. 74} \par
Wie man an diesen Beispielen sehen kann, wurden selbst in einem recht kurzen Zeitraum viele verschiedene Wortstellungen in Sätzen verwendet, zumindest in literarischen Texten. Deshalb müssen im Grunde in der Grammatik sechs verschiedene Wortstellungen ermöglicht werden: Subjekt-Verb-Objekt, Subjekt-Objekt-Verb, Verb-Subjekt-Objekt, Verb-Objekt-Subjekt, Objekt-Subjekt-Verb und Objekt-Verb-Subjekt. Dies wurde so auch umgesetzt, wobei üblicherweise nur die Wortstellung Subjekt-Objekt-Verb erzeugt wird. Die anderen Wortstellungen können möglicherweise in einer zukünftigen Version der Grammatik über die Extra-Grammatik verwendet werden.\par
In den Ressource Grammars des Grammatical Frameworks gibt es drei verschiedene Kategorien für Sätze, bei denen unterschiedliche Merkmale fixiert oder variabel sind. Bei Clauses (\texttt{Cl}) sind noch alle Tempusmerkmale, die Polarität, also ob ein Satz verneint oder nicht geäußert wird, und im Lateinischen die Wortstellung variabel. Bei Sentences (\texttt{S}), also auf der Satzebene werden die Wortstellung, die Tempusmerkmale und die Polarität festgelegt. Dabei ist die Wortstellung bisher fix und die anderen Merkmale können über Parameter gesetzt werden. Die nächste Stufe der Utterances \texttt{Utt} verhält sich an sich wie Sentences kann aber mit einer Konjunktion und einem Vokativ zu einer Phrase (\texttt{Phr}) erweitert werden. Phrases sind die übliche Startkategorie für Sätze im Grammatical Framework. Dabei werden Konjunktion und Vokativ dadurch optional, dass Objekte der entsprechende Typen gegeben sind, die die leere Zeichenkette erzeugen. \par
\begin{lstlisting}[float=h!tp,caption={Typ von Clauses (vgl. \textbf{CalLat.gf})},label={GF-Cat-Cl}]
lincat
  Cl = { s : Tense => Anteriority \
     => Polarity => Order => Str } ;
\end{lstlisting}
Der Typ eines Clauses ist eine \texttt{Str}-Tabelle, die von \texttt{Tense}, \texttt{Anteriority}, \texttt{Polarity} und \texttt{Order} abhängig ist. Dabei sind \texttt{Tense}, \texttt{Anteriority} und \texttt{Polarity} gemeinsame Parameter vieler Sprachen und deshalb auch unabhängig von diesen definiert.\footnote{vgl. \textbf{ParamX.gf} im Ordner \textbf{lib/src/common/}}. Der Parameter \texttt{Order} entspricht den oben genannten Wortstellungen (\texttt{Order = SVO | VSO | VOS | OSV | OVS | SOV} vgl. \textbf{ResLat.gf}). \par
\begin{lstlisting}[float=h!tp,caption={Syntaxregel \texttt{PredVP} um eine \texttt{NP} und eine \texttt{VP} zu einer \texttt{Cl} zu kombinieren (vgl. \textbf{SentenceLat.gf})},basicstyle=\small,label={GF-Sentence-PredVP}]
lin
  PredVP np vp = -- NP -> VP -> Cl
  {
    s = \\tense,anter,pol,order => 
      case order of {
        SVO => 
          np.s ! Nom ++ negation pol ++ 
          vp.adj ! Ag np.g Sg Nom ++ 
          vp.fin ! VAct ( anteriorityToVAnter anter )
                        ( tenseToVTense tense ) 
                        np.n np.p ++
          vp.obj ;
        VSO => negation pol ++ vp.adj ! Ag np.g Sg Nom ++ 
          vp.fin ! VAct ( anteriorityToVAnter anter ) 
                        ( tenseToVTense tense ) 
                        np.n np.p ++ 
          np.s ! Nom ++ vp.obj ;
        VOS => negation pol ++ vp.adj ! Ag np.g Sg Nom ++ 
          vp.fin ! VAct ( anteriorityToVAnter anter ) 
                        ( tenseToVTense tense ) 
                        np.n np.p ++ 
          vp.obj ++ np.s ! Nom ;
        OSV => vp.obj ++ np.s ! Nom ++ negation pol ++ 
          vp.adj ! Ag np.g Sg Nom ++ 
          vp.fin ! VAct ( anteriorityToVAnter anter ) 
                        ( tenseToVTense tense ) 
                        np.n np.p ;
        OVS => vp.obj ++ negation pol ++ 
          vp.adj ! Ag np.g Sg Nom ++
          vp.fin ! VAct ( anteriorityToVAnter anter ) 
                        ( tenseToVTense tense ) 
                        np.n np.p ++ 
          np.s ! Nom ;
        SOV => np.s ! Nom ++ vp.obj ++ negation pol ++ 
          vp.adj ! Ag np.g Sg Nom ++ 
          vp.fin ! VAct ( anteriorityToVAnter anter ) 
                        ( tenseToVTense tense ) 
                        np.n np.p 
    }
  } ;
\end{lstlisting}
Erzeugt werden Clauses durch die Regel \texttt{PredVP} (vgl. Listing \ref{GF-Sentence-PredVP}), die eine \texttt{NP} und eine \texttt{VP} zu einer Clause kombiniert. Die Kombination der Bestandteile erfolgt logischerweise bei den verschiedenen Wortstellungen in unterschiedlicher Reihenfolge. Das Vorgehen wird allerdings der Einfachheit halber nur am Beispiel der Wortstellung Subjekt-Objekt-Verb gezeigt. \par
Zunächst wird eine, von allen nötigen Merkmalen abhängige Tabelle gebildet, die nach Auswahl aller Merkmale eine Zeichenkette liefert. Diese Zeichenkette ist folgendermaßen aufgebaut: Aus der Subjekt-\texttt{NP}, die unter dem Namen \texttt{np} zu finden ist, wird die Nominativ-Form verwendet und an diese wird die im \texttt{obj}-Feld der \texttt{VP} gespeicherte Objektform angehängt. Als nächstes wird, wenn der Satz negiert ist, der Negationspartikel \textit{non} eingefügt. Dies geschieht mit Hilfe der Funktion \texttt{negation} aus \textbf{ResLat.gf}, die wenn der Wert für \texttt{pol} negativ ist, die entsprechende Zeichenkette, und wenn \texttt{pol} positiv ist, die leere Zeichenkette zurück gibt. Für den Fall, dass ein Adjektiv prädikativ gebraucht wird, ist dieses Adjektiv in der Verbalphrase im Feld \texttt{adj} abgelegt, und wird als nächstes in die Zeichenkette eingefügt. Dazu wird die Singular-Nominativ-Form des Adjektivs im Genus des Subjekts gewählt. Und zum Schluss folgt das Verb in der passenden Form. Dazu werden die Tabellenwerte für die Tempusmerkmale so wie der Numerus und das Genus des Subjekts verwendet. Da für die Verbformen eigene Typen für die Tempusmerkmale verwendet werden, die von denen in \textbf{ParamX.gf} unterschiedlich sind, müssen sie mit den Hilfsfunktionen \texttt{anteriorityToVAnter} und \texttt{tenseToVTens}\footnote{vgl. \textbf{ResLat.gf}} entsprechend umgewandelt werden. Das hat historische Gründe, denn die Verb-Merkmale wurden von den vorhergehenden Arbeiten an der Grammatik übernommen, die allerdings aus einem früheren Stand der Ressource Grammar Library zu stammen scheinen. Als auf Satzebene die Merkmale aus \texttt{ParamX} nötig waren, wurde der Einfachheit halber die beschriebene Abbildung zwischen den Merkmalen implementiert. In Zukunft sollten diese Merkmale möglichst vereinheitlicht werden. Für die anderen Wortstellungen erfolgt die Zusammensetzung der Zeichenkette analog, jedoch in anderer Reihenfolge der einzelnen Komponenten.\footnote{vgl. \textbf{SentenceLat.gf}} \par
Damit ist die erste Stufe auf dem Weg einer komplette Phrase abgeschlossen. Als nächstes Folgt die Umwandlung einer Clause in einen Sentence. Diese Umwandlung wird in der Regel \texttt{UseCl} beschrieben. Zusätzlich zu einem \texttt{Cl}-Objekt werden ein Objekt vom Typ \texttt{Temp} und ein Objekt vom Typ \texttt{Pol} benötigt. \texttt{Temp} ist eine gemeinsame Datenstruktur, die \texttt{Tense} im \texttt{t}-Feld, \texttt{Anteriority} im \texttt{a}-Feld und eine mögliche Zeichenkette im \texttt{s}-Feld zusammenfasst. Ebenso enthält \texttt{Pol} die \texttt{Polarity} in einem \texttt{p}-Feld und eine Zeichenkette im \texttt{s}-Feld\footnote{vgl. \textbf{CommonX.gf} im Ordner \textbf{lib/src/common}}. Die Funktion \texttt{UseCl} fasst diese Parameter nun so zusammen, dass sie das \texttt{s}-Feld des \texttt{Temp}-Parameter mit dem \texttt{s}-Feld des \texttt{Pol}-Parameters und dem \texttt{s}-Feld der Clause verbindet. Anzumerken ist, dass die beiden \texttt{s}-Felder üblicherweise leer sind. Also wird aus der Clause die passende Form ausgewählt, in dem \texttt{Tense} und \texttt{Anteriority}, also das \texttt{t}- und \texttt{a}-Feld aus der Variable \texttt{t}, die das \texttt{Temp}-Objekt enthält, verwendet werden. Anschließend wird anhand der Polarität aus dem \texttt{Pol}-Objekt ausgewählt und schließlich die Wortstellung \texttt{SOV} festgelegt.\footnote{vgl. \textbf{SentenceLat.gf}} \par
Nachdem auch dieser Schritt getan ist, sind nur noch zwei weitere Regeln nötig, bis Objekte vom Typ \texttt{Phr} erzeugt werden können. Zunächst muss es möglich sein Sentences in Utterances zu transformieren. Dies geschieht in der Regel \texttt{UttS} allein durch die explizite Änderung des Typs mit Hilfe des Schlüsselworts \texttt{lin}. \par
\begin{lstlisting}[float=h!tp,caption={Syntaxregeln um eine Utterance mit einer Konjunktion und einem Vokativ zu einer Phrase zu kombinieren (vgl. \textbf{PhraseLat.gf})},basicstyle=\small,label={GF-Phrase-PhrUtt}]
lin
  -- PConj -> Utt -> Voc -> Phr ;
  PhrUtt pconj utt voc = 
    {s = pconj.s ++ utt.s ++ voc.s} ;
  NoPConj = {s = []} ;
  PConjConj conj = {s = conj.s2} ; ---
  NoVoc = {s = []} ;
  VocNP np = {s = np.s ! Voc} ;
\end{lstlisting}
Die Regel \texttt{PhrUtt}, um aus einer Utterance ein \texttt{Phr}-Objekt zu erzeugen, bedarf etwas größeren Aufwands. Denn sie erweitert unter Umständen die Utterance noch um eine Konjunktion am Satzbeginn und am Schluss eine Nominalphrase im Vokativ. Dazu benötigt sie zwei zusätzliche Parameter, der eine vom Typ \texttt{PConj} und der andere vom Typ \texttt{Voc}. \par
Zum einen sind dafür die parameterlosen Funktionen \texttt{NoPConj} und \texttt{NoVoc} definiert, die einen leeren Platzhalter für diese Typen erzeugen, also Objekte, die nur die leere Zeichenkette erzeugen. Des weiteren gibt es die Regel \texttt{PConjConj} die ein \texttt{PConj}-Objekt\footnote{phrase-beginning conjunction} aus einer normalen Konjunktion erzeugt, indem es die Zeichenkette aus dem \texttt{s2}-Feld der Konjunktion, die die Zeichenkette enthält, die bei normaler Verwendung zwischen die zwei zu verbindenden Phasen gesetzt wird. Und für das Anfügen des Vokativs gibt es die Regel \texttt{VocNP}, die eine \texttt{NP} in ein \texttt{Voc}-Objekt umwandelt, indem es aus der Nominalphrase die Vokativ-Form auswählt. \par
Nun kann aus einem, möglicherweise leeren, \texttt{PConf}-Objekt, einer Utterance und einem, ebenfalls optionalen, Vokativ eine Phrase gebildet werden, in dem die drei Zeichenketten in den jeweiligen \texttt{s}-Feldern in eben dieser Reihenfolge, in der sie aufgezählt wurden, verkettet werden.\footnote{vgl. \textbf{PhraseLat.gf}} \par
Mit diesen Syntaxregeln ist das Ziel erreicht, Sätze mit der Startkategorie \texttt{Phr} zu erzeugen und zu parsen. Somit lässt sich diese Lateingrammatik, in gewissem Umfang, zusammen mit anderen Sprachen der Ressource Grammar Library verwenden.
